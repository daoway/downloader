plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'pmd'
    id 'jacoco'
    id "com.github.spotbugs" version "6.0.26"
    id "org.sonarqube" version "6.0.1.5171"
    id 'checkstyle'
    id "io.snyk.gradle.plugin.snykplugin" version "0.6.1"
}

group = 'com.blogspot.ostas.downloader'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

repositories {
    mavenCentral()
}

ext {
    set('testcontainersVersion', "1.20.4")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-hsqldb'
    runtimeOnly 'com.h2database:h2'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.openjdk.jol:jol-core:0.17'
}

tasks.named('test') {
    useJUnitPlatform()
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test
}

test {
    finalizedBy jacocoTestReport
}

pmd {
    consoleOutput = true
    toolVersion = "7.9.0"
    rulesMinimumPriority = 5
    ruleSetFiles = files("config/pmd/pmd.xml")
    ruleSets = []
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
    excludeFilter.set(file("config/spotbugs/excludeFilter.xml"))
}

checkstyle {
    toolVersion = '10.21.1'
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

snyk {
    arguments = '--all-sub-projects'
    severity = 'low'
    api = System.getenv('SNYK_TOKEN')
    autoDownload = true
    autoUpdate = true
}

tasks.withType(JavaExec) {
    jvmArgs("-Djdk.attach.allowAttachSelf")
}